/**
 * Example: Epidemic and Sick with Symptoms.
 * See below for suggested tests.
 */
//
// SORT DECLARATIONS:
sort People: 4, bob, dave, rodrigo, ciaran;

//
// RANDOM VARIABLE DECLARATIONS:
random epidemic: Boolean;
random sick: People -> Boolean;
random fever: People -> Boolean;
random rash: People -> Boolean;
random itch: People -> Boolean;

//
// RULES
// By default, how likely is an epidemic?
epidemic 0.001;

if epidemic then sick(X) 0.6 else sick(X) 0.05;
if sick(X) then fever(X) 0.7 else fever(X) 0.01;
if sick(X) then rash(X) 0.6 else rash(X) 0.07;
if rash(X) then itch(X) 0.9 else itch(X) 0.01;

// Suggested tests:

// query: sick(dave)

// rash(dave);
// query: sick(dave);

// rash(dave); fever(dave);
// query: sick(dave);

// query: epidemic

// fever(dave);
// query: epidemic

// fever(dave); fever(bob);
// query: epidemic

// sick(dave); sick(bob);
// query: epidemic

// fever(dave); fever(bob);
// query: sick(X)
// Note how answer is generic for X distinct from either bob or dave

// fever(dave); rash(dave); fever(bob);
// query: sick(X)
// Note how answer is distinct for X not in {bob, dave}, and differentiates between the two as well.

// if X = dave or X = bob then sick(X) else not sick(X);
// query: epidemic
// Note how message from sick(X) is generic for all X not in {bob, dave} and is computed only once;
// That is then exponentiated to the type size - 2 (everybody but bob and dave)
// Change population size to be that answer changes but is computed in the same time.

// if X = dave then itch(X) else not itch(X);
// query: sick(X)
// In this example, X is manipulated in an unbound way till the evidence on itch is encountered,
// when it is then differentiated.
